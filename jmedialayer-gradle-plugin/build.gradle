buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.3"
	}
}

plugins {
	id "java-gradle-plugin"
	id "java"
	id "application"
	id "maven"
	id "maven-publish"
}

apply plugin: 'com.jfrog.bintray'
apply plugin: "kotlin"

group 'jmedialayer'
version '0.1'


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
}

sourceSets {
	main.java.srcDirs = ['src']
	main.kotlin.srcDirs = ['src']
	main.resources.srcDirs = ['resources', 'assets']
	test.java.srcDirs = ['test']
	test.kotlin.srcDirs = ['test']
	test.resources.srcDirs = ['testresources', 'testassets']
}

dependencies {
	compile "com.jtransc:jtransc-rt-core:$jtranscVersion"
	compile "com.jtransc:jtransc-gradle-plugin:$jtranscVersion"
	compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.3"
	compile "org.apache.commons:commons-compress:1.12"
	testCompile group: 'junit', name: 'junit', version: '4.11'
}

gradlePlugin {
	plugins {
		simplePlugin {
			id = "jmedialayer"
			implementationClass = "jmedialayer.GradlePlugin"
		}
	}
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId 'jmedialayer'
			artifactId 'jmedialayer-gradle-plugin'
			version "$project.version"
		}
	}
}

bintray {
	// https://github.com/bintray/gradle-bintray-plugin#buildgradle
	Properties bintray = new Properties()
	bintray.load(new FileInputStream(System.getProperty("user.home") + "/.bintray.properties"))

	user = bintray.BINTRAY_USER
	key = bintray.BINTRAY_KEY
	publications = ['MyPublication']
	override = true

	pkg {
		repo = 'jmedialayer'
		name = 'jmedialayer'
		userOrg = 'soywiz'
		licenses = ['GPL-3.0']
		vcsUrl = 'https://github.com/jmedialayer/jmedialayer.git'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.0'
}